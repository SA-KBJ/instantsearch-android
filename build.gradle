// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: 'libraries.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0-alpha02'
    }
    ext.with {
        VERSION = '1.13.0'
        GROUP = 'com.algolia'
        BASENAME = 'instantsearch-android'
        CODENAME = GROUP + ":" + BASENAME
        NAME = 'InstantSearch Android'
        DESC = "A library to build instant-search applications on Android."
        GITHUB = "algolia/instantsearch-android"
        WEBSITE = "https://github.com/" + GITHUB
        REPO = WEBSITE + ".git"
        LICENSE = 'MIT'
        LICENSE_URL = "http://www.opensource.org/licenses/mit-license.php"
        VERSION_DESC = 'Version ' + VERSION
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.20.0" // exposes dependencyUpdates task
    id 'com.github.dcendents.android-maven' version "2.1" // adds support for deploying to Mvn repos.
    id "com.jfrog.bintray" version "1.8.4" // exposes bintray closure
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    android {
        compileSdkVersion 28
        buildToolsVersion '28.0.3'

        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 28
            versionCode 1
            versionName VERSION
            consumerProguardFiles "proguard-rules.pro"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        testOptions {
            unitTests {
                // Disable automatic throwing on system method calls.
                // Rationale: We don't want to mock system methods (for example JSONArray.length),
                // but rather ensure we use them correctly.
                returnDefaultValues = true

                // Required by Robolectric http://robolectric.org/getting-started/
                includeAndroidResources = true

                all {
                    testLogging {
                        events "skipped", "failed", "standardOut", "standardError"
                        exceptionFormat "full"
                        outputs.upToDateWhen { false }
                    }
                }
            }
        }
    }

    dependencies {
        testImplementation libraries.junit
        testImplementation libraries.robolectric
        testImplementation libraries.mockito
    }

    apply from: '../release.gradle'
}
